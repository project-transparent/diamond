plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.transparent'
archivesBaseName = 'diamond'

repositories {
    mavenCentral()
}

dependencies {
    // For some reason the java gradle plugin doesn't apply this properly
    implementation gradleApi()
}

gradlePlugin {
    plugins {
        diamond {
            id = 'org.transparent.diamond'
            implementationClass = 'org.transparent.diamond.DiamondPlugin'
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 8
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            // Gradle plugin plugin provides plugin artifact
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}
